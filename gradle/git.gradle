/*
 * Copyright (c) 2016-present, 贵州纳雍穿青人李裕江<1032694760@qq.com>, All Right Reserved.
 *
 */

//See https://blog.csdn.net/sinat_35073873/article/details/53263651
//将函数设置为extra属性中去，这样，加载utils.gradle的Project就能调用此文件中定义的函数了
ext {
    gitGitCommitNumber = this.&gitGitCommitNumber
    getGitLatestTag = this.&getGitLatestTag
    getGitBranch = this.&getGitBranch
    getGitLatestCommitHash = this.&getGitLatestCommitHash
    getGitLatestCommitHashShort = this.&getGitLatestCommitHashShort
}

@SuppressWarnings(["unused"])
static def gitGitCommitNumber() {
    try {
        // See https://blog.csdn.net/liumeng920/article/details/51011251
        def cmd = 'git rev-list HEAD --first-parent --count'
        def integer = cmd.execute().text.trim().toInteger()
        if (integer < 1) {
            integer = 1
        }
        return integer
    } catch (ignored) {
        return 1
    }
}

@SuppressWarnings(["unused"])
static def getGitLatestTag() {
    try {
        // See https://www.jianshu.com/p/2ecfd791c464
        def cmd = 'git describe --tags --always'
        def version = cmd.execute().text.trim() ?: '1.0.0'
        if (version.indexOf('-') != -1) {
            version = version.split('-')[0]
        } else if (version.indexOf('.') == -1) {
            version = "1.0.0"
        }
        if (!version.matches(/\d+\.\d+(\.\d+)?/)) {
            System.err.println("Version format: Major.Minor.Patch")
        }
        return version
    } catch (ignored) {
        return "0.0.0"
    }
}

@SuppressWarnings(["unused"])
static def getGitBranch() {
    try {
        // See https://www.jianshu.com/p/1f81af606e41
        def cmd = 'git symbolic-ref --short -q HEAD'
        return cmd.execute().text.trim() ?: "unknown"
    } catch (ignored) {
        return "error"
    }
}

@SuppressWarnings(["unused"])
static def getGitLatestCommitHash() {
    try {
        // See https://www.cnblogs.com/fuyaozhishang/p/7675551.html
        def cmd = 'git rev-parse HEAD'
        return cmd.execute().text.trim() ?: "unknown"
    } catch (ignored) {
        return "error"
    }
}

@SuppressWarnings(["unused"])
static def getGitLatestCommitHashShort() {
    try {
        // See https://www.jianshu.com/p/1f81af606e41
        def cmd = 'git rev-parse --short HEAD'
        return cmd.execute().text.trim() ?: "unknown"
    } catch (ignored) {
        return "error"
    }
}
